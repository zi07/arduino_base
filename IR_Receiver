=========================== 

/*
 * RawIRdecode_Mod (2016.8.19)
 * 
 * Original Sketch --> https://github.com/adafruit/Raw-IR-decoder-for-Arduino
 */
 
#define IRpin_PIN      PIND
#define IRpin          2        // Digital pin 2 of Arduino
#define MAXPULSE       65000    // Maximum listen duration (65 msec)    
#define RESOLUTION     20       // Timing resolution 
 
uint16_t pulses[100][2];        // pair is high and low pulse 
uint8_t currentPulse = 0;       // index for pulses we're storing
 

void setup(void) {
  Serial.begin(9600);
  Serial.println("Ready to decode IR!");
}

 
void loop(void) {
  uint16_t highPulse, lowPulse; // temporary storage timing
  highPulse = lowPulse = 0;     // start out with no pulse length
  
  while (IRpin_PIN & (1 << IRpin)) {
    highPulse++;
    delayMicroseconds(RESOLUTION);
    if ((highPulse >= MAXPULSE) && (currentPulse != 0)) {
      printPulses();
      currentPulse=0;
      return;
    }
  }
  pulses[currentPulse][0] = highPulse;
  
  while (! (IRpin_PIN & _BV(IRpin))) {
    lowPulse++;
    delayMicroseconds(RESOLUTION);
    if ((lowPulse >= MAXPULSE)  && (currentPulse != 0)) {
      printPulses();
      currentPulse=0;
      return;
    }
  }
  pulses[currentPulse][1] = lowPulse;
  
  currentPulse++;
}
 

void printPulses(void) {
  for (uint8_t i = 0; i < currentPulse-1; i++) {
    Serial.print(pulses[i][1] * RESOLUTION, DEC);
    Serial.print(",-");
    Serial.print(pulses[i+1][0] * RESOLUTION, DEC);
    Serial.print(",");
  }
  Serial.println(pulses[currentPulse-1][1] * RESOLUTION, DEC);
}Colored by Color Scripter
cs
=================================
